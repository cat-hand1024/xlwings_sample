Sub 분류별시트()

    Dim 현재시트 As Worksheet
    Dim 분류 As Range, 셀 As Range
    Dim 제품레코드 As Range, 복사위치 As Range
    Dim 시트이름 As String
    Dim 전체건수 As Long
    Dim 진행바너비 As Integer
    Dim 처리건수 As Long
    Dim 진행율 As Single
        
    Set 현재시트 = ActiveSheet
    Set 분류 = Range("C3", Cells(Rows.Count, "C").End(xlUp))
        
    전체건수 = 분류.Count
    진행바너비 = 245
        
    frm진행상황.Show vbModeless

    Application.ScreenUpdating = False

        For Each 셀 In 분류
        
            시트이름 = 셀.Value
                
            If Not IsSheet(시트이름) Then
            
                Worksheets.Add After:=Sheets(Sheets.Count)
                ActiveSheet.Name = 시트이름
            
                With 현재시트
                    
                    .Range("B2:E2").Copy
                    Range("B2").PasteSpecial xlPasteAll
                    Range("B2").PasteSpecial xlPasteColumnWidths
                    
                    Range("B3").Select
                    
                    Application.CutCopyMode = False
                    
                    .Activate
                    
                End With
            
            End If
            
            With Worksheets(시트이름)
            
                If .Columns(2).Find(셀.Offset(0, -1)) Is Nothing Then
                
                    Set 제품레코드 = 셀.Offset(0, -1).Resize(1, 4)
                    Set 복사위치 = .Cells(Rows.Count, "B").End(xlUp).Offset(1)
                    
                    제품레코드.Copy 복사위치
                
                End If
            
            End With
            
            처리건수 = 처리건수 + 1
            진행율 = 처리건수 / 전체건수
        
            With frm진행상황
            
                .lbl진행바.Width = 진행율 * (진행바너비)
                .lbl백분율.Caption = Format(진행율, "0%")
                .lbl백분율.Left = .lbl진행바.Left + .lbl진행바.Width
                            
            End With
        
            DoEvents
            
        Next
    
    Application.ScreenUpdating = True
    Application.OnTime Now + TimeSerial(0, 0, 1), "폼닫기"
    
End Sub